plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.diffplug.gradle.spotless' version '3.26.0' // spotless formatting for this program
    id 'com.gradle.plugin-publish' version '0.10.1'
}

project.description = 'Preconfigured spotless configuration and gradle tasks for use in other projects'
project.group = 'com.jonathanrobertson.spotless'
project.version = '1.1.0'

project.ext.name = 'auto-spotless-plugin'
project.ext.pluginId = 'com.jonathanrobertson.spotless'
project.ext.implementationClass = 'com.jonathanrobertson.spotless.AutoSpotlessPlugin'

sourceCompatibility = 1.8

spotless {
    java {
        removeUnusedImports()
        importOrderFile('src/main/resources/spotless.importorder')
        eclipse().configFile('src/main/resources/spotless.eclipseformat.xml')
    }
    format 'misc', {
        target '**/*.md', '**/*.gradle', '**/.gitignore', '**/*.yml', '**/*.yaml'
        trimTrailingWhitespace()
        endWithNewline()
        replaceRegex 'too many blank lines', '^\\n\\n+', '\n'
    }
    format 'gradle', {
        target '**/*.gradle'
        replaceRegex 'blank lines following {', '\\{\\n\\s*\\n+', '{\n'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.diffplug.spotless:spotless-plugin-gradle:3.26.0' // spotless formatting for other programs

    testCompile 'junit:junit:4.12'
}

// ref: maven-publish
publishing {
    publications {
        pluginPublication (MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.ext.name
            version project.version
        }
    }
}

// ref: com.gradle.plugin-publish
pluginBundle {
    website = 'https://github.com/jonathan-robertson/auto-spotless-plugin'
    vcsUrl = 'https://github.com/jonathan-robertson/auto-spotless-plugin'
    tags = ['spotless', 'configuration', 'style', 'format']
}

// ref: java-gradle-plugin
gradlePlugin {
    plugins {
        autoSpotlessPlugin {
            id = project.ext.pluginId
            displayName = project.ext.name
            description = project.description
            implementationClass = project.ext.implementationClass
        }
    }
}
